plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

group 'me.hardcoded.smreader'
version '1.0.0'


sourceSets {
    main {
        java.srcDirs 'src/main/java'
        resources.setSrcDirs(['src/main/resources'])
    }
    test {
        java.srcDirs 'src/test/java'
        resources.setSrcDirs(['src/test/resources'])
    }
}

configurations {
    // implementations that should be included in the jar file
    packImplementation {
        transitive false
    }

    implementation.extendsFrom(packImplementation)
}

dependencies {
    implementation 'junit:junit:4.13.2'
    testImplementation 'junit:junit:4.13.2'

    packImplementation 'org.lz4:lz4-java:1.8.0'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

test {
    failFast false
    filter {
        includeTestsMatching 'me.hardcoded.test.*'
    }
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

jar {
    from "LICENSE"
    from {
        configurations.packImplementation.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.kariaro'
            artifactId = 'ScrapMechanicReader'
            version = "${version}"

            from components.java
        }
    }
}
